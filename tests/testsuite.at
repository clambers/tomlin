# Copyright (C) 2015 Chris Lamberson.
#
# This file is part of Tomlin.
#
# Tomlin is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# Tomlin is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with Tomlin.  If not, see <http://www.gnu.org/licenses/>.

AT_INIT
AT_COPYRIGHT([Copyright (C) 2015 Chris Lamberson.])
AT_COLOR_TESTS

# AX_DO(COMMAND)
# --------------
# Run a command, failing if status != 0.
m4_define([AX_DO],
[m4_define([COMMAND], [$1])
AT_CHECK([COMMAND], [0], [ignore], [ignore])
])

# AX_INCLUDE_HEADER
# -----------------
# Standard code for including the library header.
m4_define([AX_INCLUDE_HEADER], [#include <exceptions.hh>
#include <parser.hh>])

# AX_TEST(TARGET, CODE)
# ---------------------
# Compile a C++ program and check that it exits cleanly.
m4_define([AX_TEST],
[m4_pushdef([TARGET], m4_bpatsubst([$1], [/*]))
m4_pushdef([CODE], [$2])
AT_DATA([TARGET.cc], [$2])
AX_DO([$LIBTOOL --tag=CC --mode=compile $CXX $CPPFLAGS $CXXFLAGS -c TARGET.cc])
AX_DO([$LIBTOOL --tag=CC --mode=link $CXX $LDFLAGS -o TARGET TARGET.lo $LIBS])
AX_DO([./TARGET])
m4_popdef([CODE])
m4_popdef([TARGET])
])

m4_include([library.at])
